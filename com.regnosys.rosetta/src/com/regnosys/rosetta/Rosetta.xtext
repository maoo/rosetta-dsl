grammar com.regnosys.rosetta.Rosetta with org.eclipse.xtext.common.Terminals
import "http://www.rosetta-model.com/Rosetta"
import "http://www.rosetta-model.com/RosettaSimple"
import "http://www.rosetta-model.com/RosettaExpression"

RosettaModel:
	'namespace' name=(QualifiedName | STRING) (':' RosettaDefinable)?
	
	('version' version=STRING)?
	
	imports += Import*
	
	(configurations += RosettaQualifiableConfiguration)* // TODO think about having only RootElements or only configurations
	(elements += (RosettaRootElement | RootElement))*
;

QualifiedName:
	ValidID ('.' ValidID)*
;

Import:
	'import' importedNamespace=QualifiedNameWithWildcard;

QualifiedNameWithWildcard:
	QualifiedName '.*'?;

RootElement:
	Annotation | Data | Function;

fragment Definable:
	'<'definition=STRING'>';

Annotation:
	'annotation' RosettaNamed ':' Definable?
	('[' 'prefix' prefix = ValidID ']')?
	attributes += Attribute*
;

fragment Annotations returns Annotated:
	annotations+=AnnotationRef;

fragment Synonyms*:
	synonyms+=RosettaSynonym;

fragment ClassSynonyms*:
	synonyms+=RosettaClassSynonym;

fragment References*:
	references+=RosettaDocReference;

fragment RuleReference*:
	ruleReference=RosettaRuleReference;

AnnotationRef:
	'[' annotation = [Annotation|ValidID] (attribute = [Attribute|ValidID] (qualifiers += AnnotationQualifier)*)? ']'
;

AnnotationQualifier:
	qualName = STRING '=' (qualValue= STRING | qualPath = RosettaClassAttributeCall)
;

Data:
	'type' RosettaNamed ('extends' superType = [Data|QualifiedName])? ':' Definable?
	(References|Annotations|ClassSynonyms)*
	attributes += Attribute*
	conditions += Condition*
;

Attribute:
	(override ?= 'override')? RosettaNamed RosettaTyped card=RosettaCardinality Definable? (References|Annotations|Synonyms)* RuleReference?
;

Enumeration returns RosettaEnumeration:
	'enum' RosettaNamed ('extends' superType=[RosettaEnumeration|QualifiedName])? ':' RosettaDefinable? References*
		(synonyms += RosettaSynonym)*
		enumValues += RosettaEnumValue*
;

Function:
	'func' 
	(
		RosettaNamed
		| ({FunctionDispatch} RosettaNamed '(' attribute=[Attribute|ValidID] ':' value=EnumValueReference')')
	)
	 ':' Definable?
	(References|Annotations)*
	('inputs:' inputs += Attribute+)?
	('output:' output = Attribute)?
	
	(shortcuts     += ShortcutDeclaration)*
	conditions     += Condition*
	operations     += Operation*
	postConditions += PostCondition*
;

ShortcutDeclaration:
	'alias' RosettaNamed ':' Definable?  expression=RosettaCalcExpression
;

Condition:
	'condition' RosettaNamed? ':' Definable?
		->(References|Annotations)* // conflicts with ListLiteral as RosettaCalcExpression
		( constraint = OneOf
		| constraint = Constraint
		| expression = RosettaCalcExpression)
;

Constraint:
	necessity = Necessity 'choice' attributes += [Attribute | ValidID] (',' attributes += [Attribute | ValidID])*
;


enum Necessity:
	optional | required
;

OneOf returns Constraint :
	oneOf ?= 'one-of'
;

PostCondition returns Condition:
	postCondition ?= 'post-condition'  RosettaNamed? ':' Definable?
	expression     = RosettaCalcExpression
;

Operation:
	AssignOutputOperation | OutputOperation
;

// deprecated - to be deleted once all models updated
AssignOutputOperation:
	'assign-output' assignRoot = [AssignPathRoot|ValidID] (path = Segment)? ':' Definable?
	expression = RosettaCalcExpression assignAsKey?='as-key'?
;

OutputOperation:
	('set' | add ?= 'add') assignRoot = [AssignPathRoot|ValidID] (path = Segment)? ':' Definable?
		expression = RosettaCalcExpression assignAsKey?='as-key'?
;

Segment:
	FEATURE_CALL_SEPARATOR attribute = [Attribute|ValidID] (next = Segment)?;

EnumValueReference returns RosettaEnumValueReference:
	enumeration=[RosettaEnumeration|QualifiedName] FEATURE_CALL_SEPARATOR value=[RosettaEnumValue|ValidID]
;

EmptyLiteral returns RosettaExpression:
	{ListLiteral} 'empty'
;

ListLiteral:
	{ListLiteral} '[' (elements += RosettaCalcExpression (',' elements += RosettaCalcExpression )*)? ']';

// old stuff
RosettaRootElement:
	Enumeration | RosettaBody | RosettaCorpus | RosettaSegment | 
	RosettaBasicType | RosettaRecordType | RosettaLibraryFunction | RosettaSynonymSource |
	RosettaBlueprint | RosettaQualifiedType | RosettaCalculationType  | RosettaMetaType | RosettaExternalSynonymSource |
	RosettaBlueprintReport
;



/**********************************************************************
 * Common rules 
 */
fragment RosettaDefinable:
	('<' definition=STRING '>')
;

fragment RosettaNamed:
	name = ValidID
;

fragment RosettaTyped:
	type=[RosettaType|QualifiedName]
;


ValidID:
	(ID | 'key' // used in Lib: annotations.rosetta
		| 'condition' | 'source' | 'value' | 'version' // used in CDM model
		| 'workflow' //used in the namespace
		| 'sum' //used in ListOperation
	)
;


/**********************************************************************
 * Built-in
 */ 

RosettaBasicType:
	'basicType' RosettaNamed
;

RosettaSynonymSource:
	'synonym' 'source' RosettaNamed 
;

RosettaRecordType:
	'recordType' RosettaNamed '{'
		features+=RosettaRecordFeature*
	'}'
;

RosettaRecordFeature returns RosettaTypedFeature:
	RosettaNamed type=[RosettaBuiltinType|ValidID]
;

RosettaParameter:
	RosettaNamed RosettaTyped (isArray?='[' ']')?
;

RosettaLibraryFunction returns RosettaExternalFunction:
	'library' 'function' RosettaNamed '(' (parameters+=RosettaParameter (',' parameters+=RosettaParameter)*)? ')' RosettaDefinable? RosettaTyped
;

RosettaQualifiedType:
	'qualifiedType' RosettaNamed '{'
		features+=RosettaQualifiedTypeFeature*
	'}'
;

RosettaQualifiedTypeFeature returns RosettaTypedFeature:
	RosettaNamed type=[RosettaBuiltinType|ValidID]
;

RosettaCalculationType:
	'calculationType' RosettaNamed '{'
		features+=RosettaCalculationTypeFeature*
	'}'
;

RosettaCalculationTypeFeature returns RosettaTypedFeature:
	RosettaNamed type=[RosettaBuiltinType|ValidID]
;

/**********************************************************************
 * Data model 
 */ 

RosettaMetaType:
	'metaType' RosettaNamed RosettaTyped
;

RosettaEnumValue:
	RosettaNamed ('displayName' display=STRING)? RosettaDefinable? References*
	(enumSynonyms += RosettaEnumSynonym)*
;

RosettaCardinality:
	'(' (inf=INT'..' sup=INT | inf=INT'..' unbounded?='*') ')'
;



/**********************************************************************
 * Synonyms 
 */ 

RosettaClassSynonym:
	'[' 'synonym' sources+=[RosettaSynonymSource] (',' sources += [RosettaSynonymSource])*
	 ('value' value=RosettaClassSynonymValue)?
	 ('meta' metaValue = RosettaMetaSynonymValue)?
	 ']'
;

RosettaClassSynonymValue returns RosettaSynonymValueBase:
	name = STRING (refType=RosettaSynonymRef value=INT)?
	('path' path = STRING)? 
;

RosettaSynonym:
	'[' 'synonym' sources+=[RosettaSynonymSource|QualifiedName] (',' sources += [RosettaSynonymSource])*
	body=RosettaSynonymBody
	']'
;

RosettaSynonymBody:
	( 'value'  (values+=RosettaSynonymValue ',')* values+=RosettaSynonymValue (mappingLogic=RosettaMapping)? ('meta' (metaValues += STRING ',')* metaValues += STRING)? 
		| 'hint' (hints+=STRING ',')* hints+=STRING
		| 'merge' merge=RosettaMergeSynonymValue
		| mappingLogic=RosettaMappingSetTo 
		| 'meta' (metaValues += STRING ',')* metaValues += STRING)
	
	('dateFormat' format=STRING)?
	('pattern' patternMatch=STRING patternReplace=STRING)?
	(removeHtml ?= 'removeHtml')?
	('mapper' mapper=STRING)?
;

RosettaSynonymValue returns RosettaSynonymValueBase :
	name = STRING (refType=RosettaSynonymRef value=INT)?
	('path' path = STRING)? 
	('maps' maps=INT)?
;
RosettaMetaSynonymValue  returns RosettaSynonymValueBase :
	name = STRING (refType=RosettaSynonymRef value=INT)?
	('path' path = STRING)? 
	('maps' maps=INT)?
;
RosettaMergeSynonymValue :
	name = STRING ('when' 'path' '<>' excludePath=STRING)?
;

RosettaEnumSynonym:
	/*
	 * The rationale for having a synonym which syntax is distinct for the enumeration
	 * is because it needs to accommodate integer values for FIX while there is no Tag nor ComponentID
	 * an optional definition field has also been added, while the synonymValue is of type STRING to accommodate peculiar values
	 */
	'[' 'synonym' sources+=[RosettaSynonymSource] (',' sources += [RosettaSynonymSource])*  
		'value' synonymValue=STRING ('definition' definition=STRING)?  
		('pattern' patternMatch=STRING patternReplace=STRING)? 
		(removeHtml ?= 'removeHtml')?
	']'
;

enum RosettaSynonymRef:
	tag | componentID
;

/**********************************************************************
 * Mapping Logic - Attribute Synonym
 */

RosettaMapping:
	instances += RosettaMappingInstance (',' instances += RosettaMappingInstance)*
;

RosettaMappingInstance:
	('set' 'when') when = RosettaMappingPathTests
	| (default?='default' 'to') set = RosettaMapPrimaryExpression
;

RosettaMappingSetTo returns RosettaMapping:
	instances += RosettaMappingSetToInstance (',' instances += RosettaMappingSetToInstance)*
;

RosettaMappingSetToInstance returns RosettaMappingInstance:
	('set' 'to') set = RosettaMapPrimaryExpression ('when' when = RosettaMappingPathTests)?
;

RosettaMappingPathTests:
	tests += RosettaMapTest ('and' tests += RosettaMapTest)*
;

RosettaMapTest returns RosettaExpression:
	RosettaMapPath
	| RosettaMapRosettaPath
	| RosettaMapTestExpression
;

RosettaMapPath:
	'path' '=' path=RosettaMapPathValue
;

RosettaMapRosettaPath:
	'rosettaPath' '=' path=RosettaClassAttributeCall
;

RosettaMapTestExpression returns RosettaExpression:
	RosettaMapPathValue (
		{RosettaExistsExpression.argument=current} 'exists' 
		| {RosettaAbsentExpression.argument=current} 'is' 'absent'
		| {EqualityOperation.left=current} operator=('='|'<>') right=RosettaMapPrimaryExpression
	)
;

RosettaMapPrimaryExpression returns RosettaExpression:
	EnumValueReference
	| RosettaStringLiteral
	| RosettaBooleanLiteral
	| RosettaIntLiteral
;

RosettaMapPathValue returns RosettaExpression:
	{RosettaMapPathValue} path = STRING
;


/**********************************************************************
 * Things
 */ 
 
RosettaDocReference:
	'[' ('regulatoryReference'|'docReference') body=[RosettaBody|ValidID] corpuses+=([RosettaCorpus|ValidID])+ (segments+=RosettaSegmentRef)+ ('provision' provision=STRING)? (reportedField?='reportedField')?  ']'
;

RosettaSegmentRef:
	segment=[RosettaSegment|ValidID] segmentRef=STRING
;

RosettaBody:
	('body') (bodyType=ID) RosettaNamed RosettaDefinable?
;

RosettaCorpus:
	('corpus') (corpusType=ID) (body=[RosettaBody])? (displayName=STRING)? RosettaNamed RosettaDefinable?
;

RosettaSegment:
	'segment' RosettaNamed
;


/**********************************************************************
 * Expressions 
 */ 

RosettaCallableCall: 
	callable=[RosettaCallable|QualifiedName]
	| implicitReceiver ?= 'item'
;

RosettaLiteral returns RosettaExpression:
	RosettaBooleanLiteral
	| RosettaStringLiteral
	| RosettaBigDecimalLiteral
	| RosettaIntLiteral
	| EmptyLiteral
	| ListLiteral
;

RosettaBooleanLiteral:
	value?='True' | {RosettaBooleanLiteral} 'False'
;

RosettaStringLiteral:
	value = STRING
;

RosettaBigDecimalLiteral:
	value = BigDecimal
;

RosettaIntLiteral:
	value = Integer
;

BigDecimal:
	'-'? ('.' INT | INT '.' | INT '.' INT) (('e'|'E') ('+'|'-')? INT)?
;

Integer:
	'-'? INT
;
/*
 * = '->'
 */
terminal FEATURE_CALL_SEPARATOR:
	'->'
;


RosettaClassAttributeCall returns RosettaExpression:
	RosettaClassReference ({RosettaFeatureCall.receiver=current} FEATURE_CALL_SEPARATOR feature=[RosettaFeature|ValidID])*
;

RosettaClassReference returns RosettaExpression:
	{RosettaCallableCall}
	callable=[Data|QualifiedName]
;


/**********************************************************************
 * QualifiableConfiguration
 */
RosettaQualifiableConfiguration:
	qType = RosettaQualifiableType 'root' rosettaClass = [Data|ValidID]';'
;

enum RosettaQualifiableType :
	Event = "isEvent"| Product = "isProduct"
;


/**********************************************************************
 * Function references
 */
ClosureParameter:
	name = ID
;

OptionalClosureParameter returns ClosureParameter:
	name = ID
	| {ClosureParameter}
;

FunctionReference:
	{NamedFunctionReference} function=[RosettaCallableWithArgs]
	| {InlineFunction} ((parameters+=ClosureParameter (',' parameters += ClosureParameter)*) | parameters += OptionalClosureParameter) '[' body=RosettaCalcExpression ']'
;

/**********************************************************************
 * Calculations
 */

RosettaCalcExpression returns RosettaExpression:
	RosettaCalcOr
;

/*
 * Binary operations
 */
enum CardinalityModifier:
	  ANY = 'any'
	| ALL = 'all'
;

RosettaCalcOr returns RosettaExpression:
	RosettaCalcAnd ( =>({LogicalOperation.left=current} operator='or') right=RosettaCalcAnd)*
;

RosettaCalcAnd returns RosettaExpression:
	RosettaCalcEquality ( =>({LogicalOperation.left=current} operator='and') right=RosettaCalcEquality)*
;

RosettaCalcEquality returns RosettaExpression:
	RosettaCalcComparison ( =>({EqualityOperation.left=current} (cardMod=CardinalityModifier)? operator=('='|'<>')) right=RosettaCalcComparison)*
;
	
RosettaCalcComparison returns RosettaExpression:
	RosettaCalcAdditive ( =>({ComparisonOperation.left=current} (cardMod=CardinalityModifier)? operator=(">="|"<="|">"|"<")) right=RosettaCalcAdditive)*
;

RosettaCalcAdditive returns RosettaExpression:
	RosettaCalcMultiplicative ( =>({ArithmeticOperation.left=current} operator=('+'|'-')) right=RosettaCalcMultiplicative)*
;

RosettaCalcMultiplicative returns RosettaExpression:
	RosettaCalcBinary ( =>({ArithmeticOperation.left=current} operator=('*'|'/')) right=RosettaCalcBinary)*
;

RosettaCalcBinary returns RosettaExpression:
	ListOperation (->(
		{RosettaContainsExpression.left=current} operator='contains' right=ListOperation
		| {RosettaDisjointExpression.left=current} operator='disjoint' right=ListOperation
		| {JoinOperation.left=current} operator='join' (right=ListOperation)?
	))?
;

/**
 * List operations
 */
enum ExistsModifier:
	  SINGLE='single'
	| MULTIPLE='multiple'
;

ListOperation returns RosettaExpression:
	RosettaCalcPrimary
	(
		(
			 =>({RosettaFeatureCall.receiver=current} FEATURE_CALL_SEPARATOR (=>feature=[RosettaFeature|ValidID])?)
			|=>({RosettaExistsExpression.argument=current} (modifier=ExistsModifier)? operator='exists')
			|=>({RosettaAbsentExpression.argument=current} 'is' operator='absent')
			|=>({RosettaOnlyElement.argument=current} operator='only-element')
			|=>({RosettaCountOperation.argument=current} operator='count')
			|=>({FlattenOperation.argument=current} operator='flatten')
			|=>({DistinctOperation.argument=current} operator='distinct')
			|=>({ReverseOperation.argument=current} operator='reverse')
			|=>({FirstOperation.argument=current} operator='first')
			|=>({LastOperation.argument=current} operator='last')
			|=>({SumOperation.argument=current} operator='sum')	
		) | (
			 =>({ReduceOperation.argument=current} operator='reduce')
			|=>({FilterOperation.argument=current} operator='filter')
			|=>({MapOperation.argument=current} operator=('map' | 'extract'))
			|=>({ExtractAllOperation.argument=current} operator='extract-all')
			|=>({SortOperation.argument=current} operator='sort')
			|=>({MinOperation.argument=current} operator='min')
			|=>({MaxOperation.argument=current} operator='max')
		) (functionRef=FunctionReference)?
	)*
;

/**
 * Primary operations
 */
RosettaCalcPrimary returns RosettaExpression:
	RosettaCalcConditionalExpression
	| {RosettaCallableWithArgsCall} callable=[RosettaCallableWithArgs|QualifiedName] '(' (args+=RosettaCalcExpression (',' args+=RosettaCalcExpression)*)? ')'
	| {RosettaCallableCall} callable=[AssignPathRoot|ValidID]
	| RosettaCallableCall
	| RosettaLiteral
	| '(' RosettaCalcExpression ')'
	| RosettaCalcOnlyExists
;

RosettaCalcConditionalExpression returns RosettaExpression:
	 {RosettaConditionalExpression} 'if' if=RosettaCalcExpression 'then' ifthen=RosettaCalcExpression (=>full?='else' elsethen=RosettaCalcExpression)?
;

RosettaCalcOnlyExists returns RosettaExpression:
	{RosettaOnlyExistsExpression} (args+=RosettaClassAttributeCall | ('(' args+=RosettaClassAttributeCall (',' args+=RosettaClassAttributeCall)* ')')) 'only' 'exists'
;

/*****************************************
 * Translate external synonym support
 *****************************************/

RosettaExternalSynonymSource:
	'synonym' 'source' RosettaNamed ('extends' superSynonym=[RosettaSynonymSource]) '{'
	    (externalRefs+= (RosettaExternalClass))*
	    ('enums' (externalRefs+= (RosettaExternalEnum))*)?
	'}'
;

RosettaExternalClass:
	typeRef=[Data] ':'
		(externalClassSynonyms += RosettaExternalClassSynonym)*
		(regularAttributes += RosettaExternalRegularAttribute)*
;

RosettaExternalEnum:
	typeRef = [RosettaEnumeration] ':'
		(regularValues += RosettaExternalEnumValue)*
;

RosettaExternalRegularAttribute:
	operator=('+'|'-') attributeRef=[RosettaFeature|ValidID]
		externalSynonyms += RosettaExternalSynonym*
;

RosettaExternalEnumValue: 
	operator=('+'|'-') enumRef=[RosettaEnumValue]
		externalEnumSynonyms += RosettaExternalEnumSynonym*
;

RosettaExternalClassSynonym:
	'['
	('value' value=RosettaClassSynonymValue)?
	'meta' metaValue = RosettaMetaSynonymValue
	']'
;

RosettaExternalSynonym:
	'['
	body=RosettaSynonymBody
	']'
;

RosettaExternalEnumSynonym returns RosettaEnumSynonym:
	'['
	'value' synonymValue=STRING ('definition' definition=STRING)?
	('pattern' patternMatch=STRING patternReplace=STRING)?
	']'
;


/*****************************************
 * Reporting support
 *****************************************/

RosettaRegulatoryBody:
	body=[RosettaBody|ValidID] corpuses+=([RosettaCorpus|ValidID])+
;   


/*****************************************
 * Blueprints
 */
 
RosettaBlueprintReport:
	'report' regulatoryBody=RosettaRegulatoryBody
	'in' ('real-time'|'T+1'|'T+2'|'T+3'|'T+4'|'T+5'|'ASATP')
	'when'
	eligibilityRules+=[RosettaBlueprint] ( 'and' eligibilityRules+=[RosettaBlueprint])*
	('using' 'standard' reportingStandard=[RosettaCorpus])?
	'with' 'type' reportType=[Data|QualifiedName]
;


RosettaRuleReference:
	'[' 'ruleReference' reportingRule=[RosettaBlueprint] ']'	
;

RosettaBlueprint:
 	('reporting'|'eligibility') 'rule' RosettaNamed RosettaDefinable?
	(references += RosettaDocReference)*
	(nodes = BlueprintNodeExp)?
	('output' output=[Data])?
;

BlueprintNodeExp:
	node=BlueprintNode (reference = RosettaDocReference)? ('then' next=BlueprintNodeExp)? 
;

BlueprintNode:
	(
		BlueprintRef |
		BlueprintFilter |
		BlueprintOr |
		BlueprintExtract |
		BlueprintReturn | 
		BlueprintLookup
	)
	('as' identifier=STRING)?
;

BlueprintFilter :
	'filter' 'when' (filter = RosettaCalcExpression | 'rule' filterBP = BlueprintRef)
;

BlueprintOr:
	'(' bps+=BlueprintNodeExp ( ',' bps+=BlueprintNodeExp)* ')'
;

BlueprintRef:
	blueprint=[RosettaBlueprint]
;

BlueprintExtract:
	'extract' (repeatable?='repeatable')? call=RosettaCalcExpression 
;

BlueprintReturn:
	'return' expression = RosettaCalcExpression
;

BlueprintLookup:
	'lookup' name=ID output=[RosettaType]
;
